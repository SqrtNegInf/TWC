#!/usr/bin/env raku
## run weekly challenge code for a specific task and language
# 2022-06-01 '-I lib' over-rides global precomp versions, removed from 'moar' alternative
# 2022-02-09 run both backends ('raku') or each separately ('moar', 'jvm')
# 2022-02-02 initial version

sub MAIN ( Int $ch,
           Int $task         where { $task == 1|2 },
           Str $lang is copy where { $lang eq 'raku'|'moar'|'jvm'|'perl' } ) {

  exit if 142 == $ch and 2 == $task; # not testable
  exit if 108 == $ch and 1 == $task; # not testable

  my $ext = $lang eq 'raku'|'moar'|'jvm' ?? 'raku' !! 'pl';

  if $lang eq 'raku'|'moar'|'jvm' {

    my $backend = $lang;
    $lang = 'raku';
    my $prefix-m = "$ch/$task/00-{$lang}-moar";
    my $prefix-j = "$ch/$task/00-{$lang}-jvm";

    shell qq:to/MOAR/ if $backend eq 'raku'|'moar';
        rakubrew switch moar-2023.04
        find -s $ch/$task -name '*$ext' -exec echo ' ' \\; -exec echo "===> \{\} <===" \\; -execdir raku           \{\} \\; > $prefix-m.txt 2> $prefix-m.err
        MOAR
       #find -s $ch/$task -name '*$ext' -exec echo ' ' \\; -exec echo "===> \{\} <===" \\; -execdir raku    -I lib \{\} \\; > $prefix-m.txt 2> $prefix-m.err

    shell qq:to/JVM/ if $backend eq 'raku'|'jvm';
        rakubrew switch jvm-2022.12
        find -s $ch/$task -name '*$ext' -exec echo ' ' \\; -exec echo "===> \{\} <===" \\; -execdir perl6-j -I lib \{\} \\; > $prefix-j.txt 2> $prefix-j.err
        rakubrew switch moar-2023.04
        JVM

    .IO.unlink if .IO.e and ! .IO.s for ("{$prefix-m}.err", "{$prefix-j}.err");
    .IO.unlink if .IO.e and ! .IO.s for ("{$prefix-m}.txt", "{$prefix-j}.txt");  # when the task is skipped entirely

  } else {

    my $prefix = "$ch/$task/00-{$lang}";

    shell qq:to/PERL/;
        find -s $ch/$task -name '*$ext' -exec echo ' ' \\; -exec echo "===> \{\} <===" \\; -execdir \{\} \\; > $prefix.txt 2> $prefix.err
        PERL
        # let '#!' determine which version of Perl to run
        #find -s $ch/$task -name '*$ext' -exec echo ' ' \\; -exec echo "===> \{\} <===" \\; -execdir perl \{\} \\; > $prefix.txt 2> $prefix.err

    .IO.unlink if .IO.e and ! .IO.s for "{$prefix}.err"; #, "{$prefix}.err";

  }

}
